apply plugin: 'io.github.rockerhieu.versionberg'
apply plugin: 'com.android.application'

apply plugin: 'io.fabric'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

apply from: '../config/quality/quality.gradle'

apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.getkeepsafe.dexcount'

versionberg {
    major 0
    minor 4
    nameTemplate '${major}.${minor}.${commitCount}.${commitSha}'
    codeTemplate '(((${major} * 100) + ${minor}) * 100) * 100000 + ${build}'
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.0'

    defaultConfig {
        applicationId "xyz.santeri.palmtree"
        minSdkVersion 19
        targetSdkVersion 25

        versionCode versionberg.code
        versionName versionberg.name
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix '.debug'

            ext.enableCrashlytics = false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
        textReport true
        textOutput "stdout"
        checkAllWarnings true
        showAll true
        explainIssues true
    }
}

dependencies {
    def appcompatVersion = '25.0.0'
    def daggerVersion = '2.7'
    def autovalueVersion = '1.4-rc1'
    def thirtyinchVersion = '0.8.0-rc1'
    def butterknifeVersion = '8.4.0'
    def leakcanaryVersion = '1.5'
    def toroVersion = '2.0.0-SNAPSHOT' // TODO: Should probably update (new version has API changes)

    compile "com.android.support:appcompat-v7:$appcompatVersion"
    compile "com.android.support:recyclerview-v7:$appcompatVersion"
    compile "com.android.support:cardview-v7:$appcompatVersion"
    compile "com.android.support:design:$appcompatVersion"

    compile "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    provided 'org.glassfish:javax.annotation:10.0-b28' //Required by Dagger 2

    provided "com.google.auto.value:auto-value:$autovalueVersion"
    kapt "com.google.auto.value:auto-value:$autovalueVersion"
    kapt 'com.ryanharter.auto.value:auto-value-parcel:0.2.4-rc2'

    compile "net.grandcentrix.thirtyinch:thirtyinch:$thirtyinchVersion"
    compile "net.grandcentrix.thirtyinch:thirtyinch-rx:$thirtyinchVersion"

    compile "com.jakewharton:butterknife:$butterknifeVersion"
    kapt "com.jakewharton:butterknife-compiler:$butterknifeVersion"

    debugCompile "com.squareup.leakcanary:leakcanary-android:$leakcanaryVersion"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanaryVersion"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanaryVersion"

    compile "im.ene.lab:toro:$toroVersion"
    compile "im.ene.lab:toro-player:$toroVersion"
    compile "im.ene.lab:toro-ext:$toroVersion"

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    compile 'io.reactivex:rxjava:1.2.2'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'com.jakewharton.rxbinding:rxbinding:0.4.0'

    compile 'org.jsoup:jsoup:1.10.1'

    compile 'com.jakewharton.timber:timber:4.3.1'

    compile 'org.greenrobot:eventbus:3.0.0'

    compile 'com.github.prashantsolanki3:Shoot:0.8.3'

    compile 'com.github.clockbyte:admobadapter:1.3.0'

    ///
    /// Libraries for UI
    ///

    compile "com.mikepenz:materialize:1.0.0@aar"
    compile 'me.zhanghai.android.materialprogressbar:library:1.3.0'
    compile 'com.afollestad.material-dialogs:core:0.9.1.0'
    compile 'net.xpece.android:support-preference:1.0.3'

    compile "com.mikepenz:itemanimators:0.5.0@aar"
    compile "com.andkulikov:transitionseverywhere:1.7.0"

    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.devbrackets.android:exomedia:3.1.0'
    compile "com.davemorrissey.labs:subsampling-scale-image-view:3.6.0"
    compile 'com.github.linger1216:labelview:v1.1.1'

    compile 'com.github.wseemann:FFmpegMediaMetadataRetriever:1.0.11' // TODO: Wait for fix to issue with arm64 https://github.com/wseemann/FFmpegMediaMetadataRetriever/issues/124

    // If you're wondering, this is a forked version with "fullscreen swipe" functionality
    // See: https://github.com/ikew0ng/SwipeBackLayout/pull/121
    compile project(':swipe-layout')

    ///
    /// Analytics, crash reporting etc.
    ///

    compile('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') {
        transitive = true;
    }

    compile('com.crashlytics.sdk.android:answers:1.3.10@aar') {
        transitive = true;
    }
}

kapt {
    generateStubs = true
}